if(INCLUDED_CUPCAKE_FIND_CONAN_PACKAGES)
  return()
endif()
set(INCLUDED_CUPCAKE_FIND_CONAN_PACKAGES TRUE CACHE INTERNAL "")

include(cupcake_find_package)

# Call cupcake_find_package on every requirement in conanfile.txt.
# For tool requirements, pass PRIVATE.
function(cupcake_find_conan_packages)
  set(mode NONE)
  file(STRINGS conanfile.txt lines)
  foreach(line ${lines})
    string(REGEX MATCH "^\\[([^]]+)\\]$" match "${line}")
    if(CMAKE_MATCH_1)
      string(REGEX MATCH "^(tool_)?requires$" match "${CMAKE_MATCH_1}")
      if(CMAKE_MATCH_1)
        # tool_requires
        set(mode PRIVATE)
        set(private PRIVATE)
      elseif(match)
        set(mode PUBLIC)
        set(private)
      else()
        set(mode NONE)
      endif()
      continue()
    endif()
    if(mode STREQUAL "NONE")
      continue()
    endif()
    string(REGEX MATCH "^([^/]+)/([^@]+)" match "${line}")
    if(NOT match OR CMAKE_MATCH_1 STREQUAL cupcake)
      continue()
    endif()
    cupcake_find_package(${CMAKE_MATCH_1} ${CMAKE_MATCH_2} ${private})
  endforeach()
endfunction()
